$${
if(@vendedor);
    log( );
    log(&a&l[iLukS]&9 &l&k! &9VENDEDOR &l&k! &fparou no armazem &e%#narmazem% &fe bau &e%#nbau% &4&l[OFF]);
	echo(/spawn);
    unset(@vendedor);
	unset(@afk_onchat);
    stop();
else;
    set(@vendedor);
	set(@afk_onchat);
    log( );
    log(&a&l[iLukS]&9 &l&k! &9VENDEDOR &l&k! &3&l[ON]);
    prompt(#homedelay,$$?,Quanto delay de home?);

    prompt(#nitens,$$?,Quantos itens diferentes deseja vender?);
	
    // ITENS A VENDER //
    #nloop = 1;
    do;
        prompt(#item[%#nloop%],$$i,Qual o id do item %#nloop% a vender?);
        prompt(&nome[%#nloop%],$$?,Qual o nome da home do item %#nloop% a vender?);
        inc(#nloop,1);
    while(%#nloop% <= %#nitens%);
	
    // N° DO ARMAZÉM //
    prompt(&tipo,$$?,Visitar o "mkb" ou "armazem"?);
    if(%&tipo% != "mkb");
        prompt(#narmazem,$$?,Qual o primeiro armazem para vender?);
        prompt(#narmazemfinal,$$?,Qual o ultimo armazem para vender?);
    else;
        #narmazem = 1;
        #narmazemfinal = %#narmazem%;
    endif;
	
    // BAÚ INICIAL DO ARMAZÉM - DIREÇÃO NORTE //
    prompt(#nbau,$$?,Qual o primeiro bau do armazem para vender?);
    prompt(#nbaumax,$$?,Qual o ultimo bau do armazem para vender?);
	
    #nbauoriginal = %#nbau%;
    #auxfila = %#nbau%;
    #filabau = 0;
    #ninv = 0;
	unsafe;
		do;
			dec(#auxfila,5);
			inc(#filabau,1);
		while(%#auxfila% > 0);
	endunsafe;
    #filabauoriginal = %#filabau%;
    #narmazemoriginal = %#narmazem%;
    do;
        if(%&tipo% == "mkb");
            echo(/home mkb);
        else;
            echo(/home armazem%#narmazem%);
        endif;
        wait(%#homedelay%);
		unsprint;
        keydown(jump);
        wait(500ms);
        keyup(jump);
        #posxarmazem = %XPOS%;
        #poszarmazem = %ZPOS%;
        #erroloop = 0;
        do;
            #posxarmazematual = %XPOS%;
            #difpos = %#posxarmazem% - %#posxarmazematual%;
            if(%#difpos% < 0);
                #difpos = -1*%#difpos%;
            endif;
            if(%#difpos% < (%#filabau% - 1));
                #looppos = 0;
				sprint;
                do;
					look(0,0);
					gui(inventory);
					keydown(right);
                    #posxarmazematual = %XPOS%;
                    #difpos = %#posxarmazem% - %#posxarmazematual%;
					if(%#difpos% == 1);
						unsprint;
						keydown(sneak);
					elseif(%#difpos% < 0);
                        #difpos = -1*%#difpos%;
                    endif;
                    inc(#looppos,1);
                    if(%#looppos% > 1000);
                        inc(#erroloop,1);
                        #looppos = 0;
                        break;
                    endif;
                while(%#difpos% < (%#filabau% - 1));
				unsprint;
                keyup(sneak);
                keyup(right);
                #poszarmazematual = %ZPOS%;
                if((%#erroloop% > 0) || (%#poszarmazematual% != %#poszarmazem%));
					log(&a&l[iLukS]&9 &l&k! &9VENDEDOR &l&k! &cErro detectado no armazem %#narmazem%);
					echo(/spawn);
					wait(%#homedelay%);
                    #erroloop = 0;
					inc(#narmazem,1);
					#error_detected = 1;
                    break;
                endif;
            endif;
			gui();
			unsafe;
				#multbau = %#nbau%;
				do;
					dec(#multbau,5);
				while(%#multbau% > 0);
			endunsafe;
            if(%#multbau% == -4);
                look(0,55);
            elseif(%#multbau% == -3);
                look(0,35);
            elseif(%#multbau% == -2);
                look(0,0);
            elseif(%#multbau% == -1);
                look(0,330);
            elseif(%#multbau% == 0);
                look(0,310);
            endif;
            wait(10ms);
			ifmatches(%HITID%,54|146);
				key(use);
			endif;
			do(100);
				wait(50ms);
				if(%GUI% == "GUICHEST");
					break;
				endif;
			loop;
			wait(50ms);
            if(%GUI% != "GUICHEST");
				log(&a&l[iLukS]&9 &l&k! &9VENDEDOR &l&k! &cErro detectado);
				#error_detected = 1;
				// echo(/spawn);
				// wait(%#homedelay%);
                break;
            endif;
            #possuiitens = 0;
            #nloop = 1;
            #slotsalvo = 53;
			unsafe;
				do;
					#idprocurado = %#item[%#nloop%]%;
					getslot(%#idprocurado%,#slotcheio,0);
					if((%#slotcheio% < 54) && (%#slotcheio% >= 0));
						inc(#possuiitens,1);
						if(%#slotcheio% < %#slotsalvo%);
							#slotsalvo = %#slotcheio%;
						endif;
					endif;
					inc(#nloop,1);
				while(%#nloop% <= %#nitens%);
			endunsafe;
            if(%#possuiitens% > 0);
				unsafe;
					#slotnobau = %#slotsalvo%;
					#itenspegos = 0;
					do;
						#nloop = 1;
						getslotitem(%#slotnobau%,#idnobau,#qtdnobau);
						do;
							if(%#idnobau% == %#item[%#nloop%]%);
								slotclick(%#slotnobau%,left,true);
								inc(#itenspegos,1);
								break;
							endif;
							inc(#nloop,1);
						while(%#nloop% <= %#nitens%);
						if((%#slotnobau% >= 53) || (%#itenspegos% > 36));
							break;
						else;
							inc(#slotnobau,1);
						endif;
					loop;
					#slotnoinv = 54;
					#slotvazio = 0;
					do;
						getslotitem(%#slotnoinv%,#idnoinv,#qtdnoinv);
						if(%#idnoinv% < 1);
							inc(#slotvazio,1);
						endif;
						inc(#slotnoinv,1);
					while(%#slotnoinv% <= 89);
				endunsafe;
            endif;
            gui();
            if((%#slotvazio% > 0) || (%#possuiitens% == 0));
                log(&a&l[iLukS]&9 &l&k! &9VENDEDOR &l&k! &cbau %#nbau% vazio!);
                #slotvazio = 1;
                inc(#nbau,1);
                if(%#multbau% == 0);
                    inc(#filabau,1);
                endif;
            endif;
            if(%#nbau% > %#nbaumax%);
				log( );
				log(&a&l[iLukS]&9 &l&k! &9VENDEDOR &l&k! &cArmazem %#narmazem% limpo!);
                inc(#narmazem,1);
                #filabau = %#filabauoriginal%;
                #nbau = %#nbauoriginal%;
                break;
            endif;
        while(%#slotvazio% > 0);
		if(%#error_detected% == 0);
			#nloop = 1;
			do;
				log(&a&l[iLukS]&9 &5Inventario com produtos. &l&k! &9VENDEDOR &l&k! &fpassando item &e%#item[%#nloop%]%);
				echo(/home %&nome[%#nloop%]%);
				wait(%#homedelay%);
				echo(/menuloja off);
				echo(/esconder);
				do(100);
					wait(50ms);
					ifmatches(%HITID%,68);
						break;
					endif;
				loop;
				ifmatches(%HITID%,68);
					key(attack);
				endif;
				inc(#nloop,1);
				wait(500ms);
			while(%#nloop% <= %#nitens%);
			if(%#narmazem% > %#narmazemfinal%);
				log( );
				log(&a&l[iLukS] &l&k! &9VENDEDOR &l&k! &cArmazens esvaziados!);
				echo(/spawn);
				unset(@vendedor);
				stop();
			endif;
		else;
			#error_detected = 0;
		endif;
    loop;
endif;
}$$
