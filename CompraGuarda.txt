$${
if(@compraguardamkb);
    log( );
    log(&a&l[iLukS]&9 &l&k! &9COMPRA/GUARDA MKB &l&k! &fparou no &fbau &e%#nbautransf% &4&l[OFF]);
    unset(@compraguardamkb);
	unset(@afk_onchat);
    stop();
else;
    set(@compraguardamkb);
	set(@afk_onchat);
	#loopwait = 0;
    log( );
    log(&a&l[iLukS]&9 &l&k! &9COMPRA/GUARDA MKB &l&k! &3&l[ON]);
	#ciclo = 0;
	#mult = 0;
    #nloop = 0;
	prompt(#homedelay,$$?,Quanto delay de home?);
	    
	prompt(#packs,$$?,Comprar packs? N=0 S=1);
	
	prompt(&homeitem,$$?,Qual a home para comprar?);
	prompt(#nbautransf,$$?,Qual o primeiro bau do mkb?);
	prompt(#nbautransfmax,$$?,Qual o ultimo bau do mkb?);
	
	unsafe;
		#nbautransforiginal = %#nbautransf%;
		#auxfila = %#nbautransf%;
		#filabautransf = 0;
		do;
			dec(#auxfila,5);
			inc(#filabautransf,1);
		while(%#auxfila% > 0);
		#filabautransforiginal = %#filabautransf%;
	endunsafe;
    do;
        echo(/home %&homeitem%);
        wait(%#homedelay%);
        echo(/esconder);
        echo(/menuloja off);
		if(%#packs% == 1);
			keydown(sneak);
		endif;
        #nloop = 0;
        do;
			wait(150ms);
            ifmatches(%HITID%,68);
				key(use);
			endif;
            wait(500ms);
            inc(#nloop,1);
            if(%#nloop% > 36);
                break;
            endif;
        loop;
		if(%#packs% == 1);
			keyup(sneak);
		endif;
		echo(/home mkb);
		wait(%#homedelay%);
		unsprint;
		keydown(jump);
		wait(500ms);
		keyup(jump);
		#erroloop = 0;
		#posxtransf = %XPOS%;
		#posztransf = %ZPOS%;
		do;
			#posxtransfatual = %XPOS%;
			#difpos = %#posxtransf% - %#posxtransfatual%;
			if(%#difpos% < 0);
				#difpos = -1*%#difpos%;
			endif;
			if(%#difpos% < (%#filabautransf% - 1));
				#looppos = 0;
				sprint;
				do;
					look(0,0);
					gui(inventory);
					keydown(right);
					#posxtransfatual = %XPOS%;
					#difpos = %#posxtransf% - %#posxtransfatual%;
					if(%#difpos% == 1);
						unsprint;
						keydown(sneak);
					elseif(%#difpos% < 0);
                        #difpos = -1*%#difpos%;
                    endif;
					inc(#looppos,1);
					if(%#looppos% > 1000);
						inc(#erroloop,1);
						#looppos = 0;
						break;
					endif;
				while(%#difpos% < (%#filabautransf% - 1));
				unsprint;
				keyup(sneak);
				keyup(right);
				#posztransfatual = %ZPOS%;
				if((%#erroloop% > 0) || (%#posztransfatual% != %#posztransf%));
					#erroloop = 0;
					break;
				endif;
			endif;
			gui();
			unsafe;
				#multbautransf = %#nbautransf%;
				do;
					dec(#multbautransf,5);
				while(%#multbautransf% > 0);
			endunsafe;
			if(%#multbautransf% == -4);
				look(0,55);
			elseif(%#multbautransf% == -3);
				look(0,35);
			elseif(%#multbautransf% == -2);
				look(0,0);
			elseif(%#multbautransf% == -1);
				look(0,330);
			elseif(%#multbautransf% == 0);
				look(0,310);
			endif;
			wait(10ms);
			if(%#nbautransf% > %#nbautransfmax%);
                log(&a&l[iLukS] &cDeposito cheio,&9 &l&k! &9COMPRA/GUARDA MKB &l&k! &4&l[OFF]);
                unset(@compraguardamkb);
                stop;
			endif;
			ifmatches(%HITID%,54|146);
				key(use);
			endif;
			do(300);
				wait(50ms);
				if(%GUI% == "GUICHEST");
					break;
				endif;
			loop;
			wait(50ms);
            if(%GUI% != "GUICHEST");
				log(&a&l[iLukS]&9 &l&k! &9COMPRA/GUARDA MKB &l&k! &cErro detectado);
				// echo(/spawn);
				// wait(%#homedelay%);
                break;
            endif;
			unsafe;
				#slotnoinv = 54;
                do(36);
                    slotclick(%#slotnoinv%,left,true);
                    inc(#slotnoinv,1);
                loop;
			endunsafe;
            wait(300ms);
			gui();
			gui(inventory);
			#slotnoinv = 9;
			#slotvazio = 0;
			if(%GUI% != "GUIINVENTORY");
				gui(inventory);
			endif;
			unsafe;
				do;
					getslotitem(%#slotnoinv%,#idnoinv,#qtdnoinv);
					if(%#idnoinv% <= 0);
						inc(#slotvazio,1);
					endif;
					inc(#slotnoinv,1);
				while(%#slotnoinv% <= 44);
			endunsafe;
			gui();
			if(%#slotvazio% >= 36);
				break;
			elseif(%#slotvazio% < 36);
				if(%#multbautransf% == 0);
					inc(#filabautransf,1);
				endif;
				log(&a&l[iLukS]&9 &l&k! &9COMPRA/GUARDA MKB &l&k! &cbau %#nbautransf% cheio!);
				inc(#nbautransf,1);
			endif;
		loop;
	loop;
endif;
}$$