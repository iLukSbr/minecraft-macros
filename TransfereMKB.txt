$${
if(@transferemkb);
    log( );
    log(&a&l[Macro]&9 &l&k! &9TRANSFERE MKB &l&k! &fparou no deposito &e%#ndeposito% e &fbau &e%#nbautransf% &4&l[OFF]);
    unset(@transferemkb);
    stop();
else;
    set(@transferemkb);
	#loopwait = 0;
	#home_delay = 2;
    log( );
    log(&a&l[Macro]&9 &l&k! &9TRANSFERE MKB &l&k! &3&l[ON]);
	prompt(#iditem,$$i,Qual o id do item que devo transferir?);
	prompt(&homeitem,$$?,Qual a home da mkb de item que devo transferir?);
	prompt(#nbau,$$?,Qual o primeiro bau do mkb a esvaziar?);
	prompt(#nbaumax,$$?,Qual o ultimo bau do mkb a esvaziar?);
	prompt(&hometransf,$$?,Qual o prefixo das homes a encher?);
	prompt(#nbautransf,$$?,Qual o primeiro bau do %&hometransf%X a encher?);
	prompt(#nbautransfmax,$$?,Qual o ultimo bau do %&hometransf%X a encher?);
	prompt(#ndeposito,$$?,Qual o numero do primeiro %&hometransf%X a encher?);
	prompt(#ndepositomax,$$?,Qual o numero do ultimo %&hometransf%X que devo encher?);
	#nbauoriginal = %#nbau%;
	#ndepositooriginal = %#ndeposito%;
	#nbautransforiginal = %#nbautransf%;
	#ciclo = 0;
	#mult = 0;
	#auxfila = %#nbau%;
	#filabau = 0;
	do;
		dec(#auxfila,5);
		inc(#filabau,1);
	while(%#auxfila% > 0);
	#filabauoriginal = %#filabau%;
	#auxfila = %#nbautransf%;
	#filabautransf = 0;
	do;
		dec(#auxfila,5);
		inc(#filabautransf,1);
	while(%#auxfila% > 0);
	#filabautransforiginal = %#filabautransf%;
	do;
		echo(/home %&homeitem%);
		wait(%#home_delay%);
		keydown(jump);
		wait(500ms);
		keyup(jump);
		#posxdeposito = %XPOS%;
		#poszdeposito = %ZPOS%;
		#erropos = 0;
		#nitens = 0;
		#invcheio = 0;
		gui(inventory);
		#slotnoinv = 9;
		#nitens = 0;
		do;
			wait(200ms);
			inc(#loopwait,1);
			if(%#loopwait% > 20);
				gui(inventory);
				#loopwait = 0;
			endif;
		until(%GUI% == "GUIINVENTORY");
		unsafe;
			do;
				getslotitem(%#slotnoinv%,#idnoinv,#qtdnoinv);
				if(%#idnoinv% > 0);
					if(%#idnoinv% == %#iditem%);
						inc(#nitens,1);
					else;
						inc(#invcheio,1);
					endif;
				endif;
				inc(#slotnoinv,1);
			while(%#slotnoinv% <= 44);
		endunsafe;
		gui();
		#nitensmax = (36 - %#invcheio%) - %#nitens%;
		log(&a&l[Macro]&9 &l&k! &9TRANSFERE MKB &l&k! &ccabem %#nitensmax% packs de itens);
		#erroloop = 0;
		do;
			if(%#nitensmax% <= 0);
				break;
			endif;
			#posxdepositoatual = %XPOS%;
			#difpos = %#posxdeposito% - %#posxdepositoatual%;
			if(%#difpos% < 0);
				#difpos = -1*%#difpos%;
			endif;
			if(%#difpos% < (%#filabau% - 1));
				#looppos = 0;
				keydown(right);
				do;
					#posxdepositoatual = %XPOS%;
					#difpos = %#posxdeposito% - %#posxdepositoatual%;
					if(%#difpos% < 0);
						#difpos = -1*%#difpos%;
					endif;
					inc(#looppos,1);
					if(%#looppos% > 2000);
						inc(#erroloop,1);
						#looppos = 0;
						break;
					endif;
				while(%#difpos% < (%#filabau% - 1));
				keyup(right);
				#poszdepositoatual = %ZPOS%;
				if((%#erroloop% > 0) || (%#poszdepositoatual% != %#poszdeposito%));
					#erroloop = 0;
					break;
				endif;
			endif;
			#multbau = %#nbau%;
			do;
				dec(#multbau,5);
			while(%#multbau% > 0);
			if(%#multbau% == -4);
				look(0,55);
			elseif(%#multbau% == -3);
				look(0,35);
			elseif(%#multbau% == -2);
				look(0,0);
			elseif(%#multbau% == -1);
				look(0,330);
			elseif(%#multbau% == 0);
				look(0,310);
			endif;
			wait(500ms);
			key(use);
			do;
				wait(200ms);
				inc(#loopwait,1);
				if(%#loopwait% > 400);
					echo(/spawn);
					wait(%#home_delay%);
					break;
				endif;
			while(%GUI% != "GUICHEST");
			if(%#loopwait% > 400);
				#loopwait = 0;
				break;
			endif;
			#slotnobau = 0;
			unsafe;
				do;
					getslotitem(%#slotnobau%,#idnobau,#qtdnobau);
					getslot(%#iditem%,#tanobau,0);
					if(%#tanobau% < 0);
						if(%#multbau% == 0);
							inc(#filabau,1);
						endif;
						gui();
						log(&a&l[Macro]&9 &l&k! &9TRANSFERE MKB &l&k! &cbau %#nbau% vazio!);
						inc(#nbau,1);
						break();
					elseif(%#idnobau% == %#iditem%);
						slotclick(%#slotnobau%,left,true);
						inc(#nitens,1);
					endif;
					if(%#nitens% == %#nitensmax%);
						log(&a&l[Macro]&9 &l&k! &9TRANSFERE MKB &l&k! &cinventario cheio!);
						gui();
						break();
					elseif(%#slotnobau% > 52);
						if(%#multbau% == 0);
							inc(#filabau,1);
						endif;
						gui();
						log(&a&l[Macro]&9 &l&k! &9TRANSFERE MKB &l&k! &cbau %#nbau% vazio!);
						inc(#nbau,1);
						break();
					else;
						inc(#slotnobau,1);
					endif;
				loop;
			endunsafe;
			if(%#nbau% > %#nbaumax%);
				log( );
				log(&a&l[Macro] &cAcabaram os itens &0=>&9 &l&k! &9TRANSFERE MKB &l&k! &l&4[REINICIANDO]);
				#ndeposito = %#ndepositooriginal%;
				#nbau = %#nbauoriginal%;
				#filabau = %#filabauoriginal%;
				#nbautransf = %#nbautransforiginal%;
				#filabautransf = %#filabautransforiginal%;
				break;
			endif;
		while(%#nitens% < %#nitensmax%);
		echo(/home %&hometransf%%#ndeposito%);
		wait(%#home_delay%);
		keydown(jump);
		wait(500ms);
		keyup(jump);
		#erroloop = 0;
		#posxtransf = %XPOS%;
		#posztransf = %ZPOS%;
		do;
			#posxtransfatual = %XPOS%;
			#difpos = %#posxtransf% - %#posxtransfatual%;
			if(%#difpos% < 0);
				#difpos = -1*%#difpos%;
			endif;
			if(%#difpos% < (%#filabautransf% - 1));
				#looppos = 0;
				keydown(right);
				do;
					#posxtransfatual = %XPOS%;
					#difpos = %#posxtransf% - %#posxtransfatual%;
					if(%#difpos% < 0);
						#difpos = -1*%#difpos%;
					endif;
					inc(#looppos,1);
					if(%#looppos% > 2000);
						inc(#erroloop,1);
						#looppos = 0;
						break;
					endif;
				while(%#difpos% < (%#filabautransf% - 1));
				keyup(right);
				#posztransfatual = %ZPOS%;
				if((%#erroloop% > 0) || (%#posztransfatual% != %#posztransf%));
					#erroloop = 0;
					break;
				endif;
			endif;
			#multbautransf = %#nbautransf%;
			do;
				dec(#multbautransf,5);
			while(%#multbautransf% > 0);
			if(%#multbautransf% == -4);
				look(0,55);
			elseif(%#multbautransf% == -3);
				look(0,35);
			elseif(%#multbautransf% == -2);
				look(0,0);
			elseif(%#multbautransf% == -1);
				look(0,330);
			elseif(%#multbautransf% == 0);
				look(0,310);
			endif;
			wait(500ms);
			if(%#nbautransf% > %#nbautransfmax%);
				inc(#ndeposito,1);
				if(%#ndeposito% > %#ndepositomax%);
					log(&a&l[Macro] &cTodos os depositos cheios,&9 &l&k! &9TRANSFERE MKB &l&k! &4&l[REINICIANDO]);
					#ndeposito = %#ndepositooriginal%;
					#nbau = %#nbauoriginal%;
					#filabau = %#filabauoriginal%;
				endif;
				#nbautransf = %#nbautransforiginal%;
				#filabautransf = %#filabautransforiginal%;
				log( );
				log(&a&l[Macro] &cDeposito cheio! Mudando para o deposito %#ndeposito%);
				break;
			endif;
			key(use);
			do;
				wait(200ms);
				inc(#loopwait,1);
				if(%#loopwait% > 400);
					echo(/spawn);
					wait(%#home_delay%);
					break;
				endif;
			while(%GUI% != "GUICHEST");
			if(%#loopwait% > 400);
				#loopwait = 0;
				break;
			endif;
			#contaloop = 0;
			unsafe;
				do;
					getslot(%#iditem%,#slotnoinv,54);
					slotclick(%#slotnoinv%,left,true);
					inc(#contaloop,1);
					if(%#contaloop% > 45);
						break;
					endif;
				while(%#slotnoinv% >= 0);
			endunsafe;
			gui();
			gui(inventory);
			#slotnoinv = 9;
			#slotvazio = 0;
			do;
				wait(200ms);
				inc(#loopwait,1);
				if(%#loopwait% > 20);
					gui(inventory);
					#loopwait = 0;
				endif;
			until(%GUI% == "GUIINVENTORY");
			unsafe;
				do;
					getslotitem(%#slotnoinv%,#idnoinv,#qtdnoinv);
					if(%#idnoinv% != %#iditem%);
						inc(#slotvazio,1);
					endif;
					inc(#slotnoinv,1);
				while(%#slotnoinv% <= 44);
			endunsafe;
			gui();
			if(%#slotvazio% >= 36);
				break;
			elseif(%#slotvazio% < 36);
				if(%#multbautransf% == 0);
					inc(#filabautransf,1);
				endif;
				log(&a&l[Macro]&9 &l&k! &9TRANSFERE MKB &l&k! &cbau %#nbautransf% cheio!);
				inc(#nbautransf,1);
			endif;
		loop;
	loop;
endif;
}$$