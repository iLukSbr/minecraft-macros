$${
if(@transferemkb);
    log( );
    log(&a&l[iLukS]&9 &l&k! &9TransfereMKB &l&k! &fparou no deposito &e%#ndeposito% e &fbau &e%#nbautransf% &4&l[OFF]);
    unset(@transferemkb);
	unset(@afk_onchat);
    stop();
else;
    set(@transferemkb);
	set(@afk_onchat);
	#loopwait = 0;
    log( );
    log(&a&l[iLukS]&9 &l&k! &9TransfereMKB &l&k! &3&l[ON]);
	prompt(#homedelay,$$?,Quanto delay de home?);
	
	prompt(#iditem,$$i,Qual o id do item que devo transferir?);
	
	prompt(&homeitem,$$?,Qual o prefixo das homes que devo esvaziar?);
	prompt(#nbau,$$?,Qual o primeiro bau do %&homeitem%X a esvaziar?);
	prompt(#nbaumax,$$?,Qual o ultimo bau do %&homeitem%X a esvaziar?);
	prompt(#nitem,$$?,Qual o numero do primeiro %&homeitem%X a esvaziar?);
	prompt(#nitemmax,$$?,Qual o numero do ultimo %&homeitem%X que devo esvaziar?);
	
	prompt(&hometransf,$$?,Qual o prefixo das homes a encher?);
	prompt(#nbautransf,$$?,Qual o primeiro bau do %&hometransf%X a encher?);
	prompt(#nbautransfmax,$$?,Qual o ultimo bau do %&hometransf%X a encher?);
	prompt(#ndeposito,$$?,Qual o numero do primeiro %&hometransf%X a encher?);
	prompt(#ndepositomax,$$?,Qual o numero do ultimo %&hometransf%X que devo encher?);
	unsafe;
		#maximolooporigem = 2*%#nbaumax%;
		#maximoloopdestino = 2*%#nbautransfmax%;
		#nbauoriginal = %#nbau%;
		#ndepositooriginal = %#ndeposito%;
		#nbautransforiginal = %#nbautransf%;
		#ciclo = 0;
		#mult = 0;
		#auxfila = %#nbau%;
		#filabau = 0;
		do;
			dec(#auxfila,5);
			inc(#filabau,1);
		while(%#auxfila% > 0);
		#filabauoriginal = %#filabau%;
		#auxfila = %#nbautransf%;
		#filabautransf = 0;
		do;
			dec(#auxfila,5);
			inc(#filabautransf,1);
		while(%#auxfila% > 0);
		#filabautransforiginal = %#filabautransf%;
	endunsafe;
	do;
		echo(/home %&homeitem%);
		wait(%#homedelay%);
		unsprint;
		keydown(jump);
		wait(500ms);
		keyup(jump);
		#posxdeposito = %XPOS%;
		#poszdeposito = %ZPOS%;
		#erropos = 0;
		#nitens = 0;
		#invcheio = 0;
		gui(inventory);
		unsafe;
			do(300);
				wait(50ms);
				if(%GUI% == "GUIINVENTORY");
					break;
				endif;
			loop;
			#slotnoinv = 9;
			#nitens = 0;
			do;
				getslotitem(%#slotnoinv%,#idnoinv,#qtdnoinv);
				if(%#idnoinv% > 0);
					if(%#idnoinv% == %#iditem%);
						inc(#nitens,1);
					else;
						inc(#invcheio,1);
					endif;
				endif;
				inc(#slotnoinv,1);
			while(%#slotnoinv% <= 44);
		endunsafe;
		gui();
		#nitensmax = (36 - %#invcheio%) - %#nitens%;
		log(&a&l[iLukS]&9 &l&k! &9TransfereMKB &l&k! &ccabem %#nitensmax% packs de itens);
		#erroloop = 0;
		#mudou_fila = 0;
		do;
			if(%#nitensmax% <= 0);
				break;
			endif;
			#posxdepositoatual = %XPOS%;
			#difpos = %#posxdeposito% - %#posxdepositoatual%;
			if(%#difpos% < 0);
				#difpos = -1*%#difpos%;
			endif;
			if(%#difpos% < (%#filabau% - 1));
				#looppos = 0;
				sprint;
				do;
					look(0,0);
					gui(inventory);
					keydown(right);
					#posxdepositoatual = %XPOS%;
					#difpos = %#posxdeposito% - %#posxdepositoatual%;
					if(%#difpos% == 1);
						unsprint;
						keydown(sneak);
					elseif(%#difpos% < 0);
                        #difpos = -1*%#difpos%;
                    endif;
					inc(#looppos,1);
					if(%#looppos% > %#maximolooporigem%);
						inc(#erroloop,1);
						#looppos = 0;
						break;
					endif;
				while(%#difpos% < (%#filabau% - 1));
				unsprint;
				keyup(sneak);
				keyup(right);
				#poszdepositoatual = %ZPOS%;
				if((%#erroloop% > 0) || (%#poszdepositoatual% != %#poszdeposito%));
					log(&a&l[iLukS]&9 &l&k! &9TransfereMKB &l&k! &cErro detectado);
					#erroloop = 0;
					echo(/spawn);
					wait(%#homedelay%);
					break;
				endif;
			endif;
			gui();
			unsafe;
				if(%#mudou_fila% == 1);
					#mudou_fila = 0;
					if(%#posxdepositoatual% <= %#posx_anterior%);
						log(&a&l[iLukS]&9 &l&k! &9TransfereMKB &l&k! &cErro detectado: Nao andou no deposito);
						echo(/home trava);
						wait(%#homedelay%);
						break;
					endif;
				endif;
				#multbau = %#nbau%;
				do;
					dec(#multbau,5);
				while(%#multbau% > 0);
			endunsafe;
			if(%#multbau% == -4);
				look(0,55);
			elseif(%#multbau% == -3);
				look(0,35);
			elseif(%#multbau% == -2);
				look(0,0);
			elseif(%#multbau% == -1);
				look(0,330);
			elseif(%#multbau% == 0);
				look(0,310);
			endif;
			wait(10ms);
			ifmatches(%HITID%,54|146);
				key(use);
			else;
				keydown(right);
				do(100);
					ifmatches(%HITID%,54|146);
						key(use);
						break;
					endif;
				loop;
				keyup(right);
			endif;
			do(300);
				wait(50ms);
				if(%GUI% == "GUICHEST");
					break;
				endif;
			loop;
			wait(50ms);
            if(%GUI% != "GUICHEST");
				log(&a&l[iLukS]&9 &l&k! &9TransfereMKB &l&k! &cErro detectado no mkb &l&4[REINICIANDO]);
				#nbau = %#nbauoriginal%;
				#filabau = %#filabauoriginal%;
				// echo(/spawn);
				// wait(%#homedelay%);
                break;
            endif;
			unsafe;
				#slotnobau = 0;
				do;
					getslotitem(%#slotnobau%,#idnobau,#qtdnobau);
					getslot(%#iditem%,#tanobau,0);
					if(%#tanobau% < 0);
						if(%#multbau% == 0);
							inc(#filabau,1);
							log(&a&l[iLukS]&9 &l&k! &9TransfereMKB &l&k! &fFila &e%#filabau% &fdo armazem);
							#mudou_fila = 1;
							#posx_anterior = %#posxdepositoatual%;
						endif;
						gui();
						log(&a&l[iLukS]&9 &l&k! &9TransfereMKB &l&k! &cbau %#nbau% vazio!);
						inc(#nbau,1);
						break();
					elseif(%#idnobau% == %#iditem%);
						slotclick(%#slotnobau%,left,true);
						inc(#nitens,1);
					endif;
					if(%#nitens% == %#nitensmax%);
						log(&a&l[iLukS]&9 &l&k! &9TransfereMKB &l&k! &cinventario cheio!);
						gui();
						break();
					elseif(%#slotnobau% > 52);
						if(%#multbau% == 0);
							inc(#filabau,1);
							log(&a&l[iLukS]&9 &l&k! &9TransfereMKB &l&k! &fFila &e%#filabau% &fdo armazem);
							#mudou_fila = 1;
							#posx_anterior = %#posxdepositoatual%;
						endif;
						gui();
						log(&a&l[iLukS]&9 &l&k! &9TransfereMKB &l&k! &cbau %#nbau% vazio!);
						inc(#nbau,1);
						break();
					else;
						inc(#slotnobau,1);
					endif;
				loop;
			endunsafe;
			if(%#nbau% > %#nbaumax%);
				log( );
				log(&a&l[iLukS] &cAcabaram os itens &0=>&9 &l&k! &9TransfereMKB &l&k! &l&4[REINICIANDO]);
				#nbau = %#nbauoriginal%;
				#filabau = %#filabauoriginal%;
				break;
			endif;
		while(%#nitens% < %#nitensmax%);
		echo(/home %&hometransf%%#ndeposito%);
		wait(%#homedelay%);
		unsprint;
		keydown(jump);
		wait(500ms);
		keyup(jump);
		#erroloop = 0;
		#posxtransf = %XPOS%;
		#posztransf = %ZPOS%;
		do;
			#posxtransfatual = %XPOS%;
			#difpos = %#posxtransf% - %#posxtransfatual%;
			if(%#difpos% < 0);
				#difpos = -1*%#difpos%;
			endif;
			if(%#difpos% < (%#filabautransf% - 1));
				#looppos = 0;
				sprint;
				do;
					look(0,0);
					gui(inventory);
					keydown(right);
					#posxtransfatual = %XPOS%;
					#difpos = %#posxtransf% - %#posxtransfatual%;
					if(%#difpos% == 1);
						unsprint;
						keydown(sneak);
					elseif(%#difpos% < 0);
                        #difpos = -1*%#difpos%;
                    endif;
					inc(#looppos,1);
					if(%#looppos% > %#maximoloopdestino%);
						inc(#erroloop,1);
						#looppos = 0;
						break;
					endif;
				while(%#difpos% < (%#filabautransf% - 1));
				unsprint;
				keyup(sneak);
				keyup(right);
				#posztransfatual = %ZPOS%;
				if((%#erroloop% > 0) || (%#posztransfatual% != %#posztransf%));
					log(&a&l[iLukS]&9 &l&k! &9TransfereMKB &l&k! &cErro detectado no %&hometransf%%#ndeposito% &4&l[PASSANDO PARA O PROXIMO]);
					inc(#ndeposito,1);
					#nbautransf = %#nbautransforiginal%;
					#filabautransf = %#filabautransforiginal%;
					if(%#ndeposito% > %#ndepositomax%);
						log(&a&l[iLukS] &cTodos os depositos cheios,&9 &l&k! &9TransfereMKB &l&k! &4&l[REINICIANDO]);
						#ndeposito = %#ndepositooriginal%;
					endif;
					#erroloop = 0;
					echo(/spawn);
					wait(%#homedelay%);
					break;
				endif;
			endif;
			gui();
			unsafe;
				if(%#mudou_fila% == 1);
					#mudou_fila = 0;
					if(%#posxtransfatual% <= %#posx_anterior%);
						log(&a&l[iLukS]&9 &l&k! &9TransfereMKB &l&k! &cErro detectado: Nao andou no transferido);
						echo(/home trava);
						wait(%#homedelay%);
						break;
					endif;
				endif;
				#multbautransf = %#nbautransf%;
				do;
					dec(#multbautransf,5);
				while(%#multbautransf% > 0);
			endunsafe;
			if(%#multbautransf% == -4);
				look(0,55);
			elseif(%#multbautransf% == -3);
				look(0,35);
			elseif(%#multbautransf% == -2);
				look(0,0);
			elseif(%#multbautransf% == -1);
				look(0,330);
			elseif(%#multbautransf% == 0);
				look(0,310);
			endif;
			wait(10ms);
			if(%#nbautransf% > %#nbautransfmax%);
				#nanterior = %#ndeposito%;
				inc(#ndeposito,1);
				if(%#ndeposito% > %#ndepositomax%);
					log(&a&l[iLukS] &cTodos os depositos cheios,&9 &l&k! &9TransfereMKB &l&k! &4&l[REINICIANDO]);
					#ndeposito = %#ndepositooriginal%;
					#nbau = %#nbauoriginal%;
					#filabau = %#filabauoriginal%;
				endif;
				#nbautransf = %#nbautransforiginal%;
				#filabautransf = %#filabautransforiginal%;
				log( );
				log(&a&l[iLukS] &l&k! &9TransfereMKB &l&k! &c%&hometransf%%#nanterior% cheio! Mudando para o %&hometransf%%#ndeposito%);
				break;
			endif;
			ifmatches(%HITID%,54|146);
				key(use);
			else;
				keydown(right);
				do(100);
					ifmatches(%HITID%,54|146);
						key(use);
						break;
					endif;
				loop;
				keyup(right);
			endif;
			do(300);
				wait(50ms);
				if(%GUI% == "GUICHEST");
					break;
				endif;
			loop;
			wait(50ms);
            if(%GUI% != "GUICHEST");
				log(&a&l[iLukS]&9 &l&k! &9TransfereMKB &l&k! &cErro detectado);
				// echo(/spawn);
				// wait(%#homedelay%);
                break;
            endif;
			unsafe;
				#contaloop = 0;
				do;
					getslot(%#iditem%,#slotnoinv,54);
					slotclick(%#slotnoinv%,left,true);
					inc(#contaloop,1);
					if(%#contaloop% > 45);
						break;
					endif;
				while(%#slotnoinv% >= 0);
			endunsafe;
			gui();
			gui(inventory);
			unsafe;
				do(300);
					wait(50ms);
					if(%GUI% == "GUIINVENTORY");
						break;
					endif;
				loop;
				#slotnoinv = 9;
				#slotvazio = 0;
				do;
					getslotitem(%#slotnoinv%,#idnoinv,#qtdnoinv);
					if(%#idnoinv% != %#iditem%);
						inc(#slotvazio,1);
					endif;
					inc(#slotnoinv,1);
				while(%#slotnoinv% <= 44);
			endunsafe;
			gui();
			unsafe;
				if(%#slotvazio% >= 36);
					break;
				elseif(%#slotvazio% < 36);
					if(%#multbautransf% == 0);
						inc(#filabautransf,1);
							log(&a&l[iLukS]&9 &l&k! &9TransfereMKB &l&k! &fFila &e%#filabautransf% &fdo transferido);
							#mudou_fila = 1;
							#posx_anterior = %#posxtransfatual%;
					endif;
					log(&a&l[iLukS]&9 &l&k! &9TransfereMKB &l&k! &cbau %#nbautransf% cheio!);
					inc(#nbautransf,1);
				endif;
			endunsafe;
		loop;
	loop;
endif;
}$$