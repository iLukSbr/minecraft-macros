$${

//////////////////////////
// CraftLandia 1.5.2    //
//////////////////////////
// Versao deste código: //
// 0.4                  //
//////////////////////////
//      NAO EDITAR      //
//////////////////////////

IFMATCHES("%CHATCLEAN%","^»Bem vindo de volta. Por favor digite /login sua-senha.$");
    SETLABEL("INFO","");
    SETLABEL("G","");
    SETLABEL("G2","");
    SET(info_stop);
    UNSET(g_chat);
    UNSET(call);
        IF(%@&sleep_password%=NULL);
        ELSE;
            ECHO("/login %@&sleep_password%");
            GUI();
            WAIT(1);
            ECHO("/tag");
            DO;
                WAIT(1);
                ECHO("/tag");
            UNTIL(%GUI%=="GUICHEST");
            WAIT(500ms);
            SLOTCLICK(2,l,true);
            WAIT(1);
            SLOTCLICK(2,l,true);
            WAIT(2);
            ECHO("/tag");
            DO;
                WAIT(1);
                ECHO("/tag");
            UNTIL(%GUI%=="GUICHEST");
            WAIT(500ms);
            SLOTCLICK(4,l,true);
            WAIT(1);
            SLOTCLICK(2,l,true);
            WAIT(2);
            ECHO("/supervisao");
            WAIT(2);
            ECHO("/pvp off");
        ENDIF;
ENDIF;
IFMATCHES("%CHATCLEAN%","^. Agora voc. est. logado.");
    SET(&dmc,"XSXLXEXEXPX");
    REPLACE(&dmc,"X","");
    LOG("");    
        IF(%@&sleep_password%=NULL);
            LOG("&6&l[&f&oI&fnfo&6&l] &aDigite \"&fsleep ipass&a\" no chat local para configurar o login automático. &9&o#%&dmc%");
        ELSE;
            LOG("&6&l[&f&oI&fnfo&6&l] &aLogin realizado. &9&o#%&dmc%");
        ENDIF;
    LOG("");
    UNSET(sistema_call);
    UNSET(info_stop);
        DO;
            SET(&label_info,"");
            SET(&label_info,%&label_info% &aFPS: &e%FPS%\n&bX: &f%&label_info%%XPOS% &bY: &f%&label_info%%YPOS% &bZ: &f%&label_info%%ZPOS%\n);
            SETLABEL("INFO","%&label_info%");
        UNTIL(info_stop);
ENDIF;

IFMATCHES("%CHATCLEAN%","^\[(G|L|A)\] (.*)(%PLAYER%)(.*)\: (.*)$");
    MATCH("%CHATCLEAN%","^\[(G|L|A)\] (.*)(%PLAYER%)(.*)\: (.*)$",{&chat_canal,&chat_unbug,&chat_nick,,&chat_msg});
        IFCONTAINS("%&chat_unbug%",":");
        ELSE;
            IFMATCHES("%&chat_msg%","sleep ipass");
                LOG("&6&l[&f&oI&fnfo&6&l] &aDigite a sua senha (totalmente sigiloso). &9&o#%&dmc%");
                LOG("");
                SET(@&sleep_password,"");
                PROMPT(@&sleep_password,$$?,"Digite a sua senha (totalmente sigiloso).");
                    IF(%@&sleep_password%=NULL);
                        LOG("&6&l[&f&oI&fnfo&6&l] &cVocê não digitou a sua senha. &9&o#%&dmc%");
                    ELSE;
                        LOG("&6&l[&f&oI&fnfo&6&l] &aSenha salva com sucesso. &9&o#%&dmc%");
                    ENDIF;
            ENDIF;
            IF(%&chat_canal%="G");
                SET(#chat_delay,"15");
                    IF(!g_chat);
                        SET(g_chat);
                            DO;
                                    DO(10);
                                        SETLABEL("G","&7[G] &c%#chat_delay%");
                                        SETLABEL("G2","&7[G] &c%#chat_delay%");
                                        WAIT(15MS);
                                    UNTIL(!g_chat);
                                    IF(!g_chat);
                                        BREAK;
                                    ENDIF;
                                DEC(#chat_delay,1);
                            WHILE(%#chat_delay%>-1);
                        UNSET(g_chat);
                    ENDIF;
                SETLABEL("G","");
                SETLABEL("G2","");
            ENDIF;
        ENDIF;
ENDIF;
IFMATCHES("%CHATCLEAN%","^\(Mensagem de ([\d\w_-]+)\): (.*)$");
    MATCH("%CHATCLEAN%","^\(Mensagem de ([\d\w_-]+)\): (.*)$",{&nick_tell,&msg_tell});
	TIME(&date,"dd/MM/yyyy HH:mm:ss");
	LOGTO("tells.txt","[%&date%] %&nick_tell%: %&msg_tell%");
	IF(@afk_onchat);
		IFMATCHES("%@&afk_onchat_bn%","%&nick_tell%");
		ELSE;
			ECHO("/r estou afk no momento, salvei sua mensagem. #%&dmc%");
			SET(@&afk_onchat_bn,"%&nick_tell%");
		ENDIF;
	ENDIF;
ENDIF;
IFMATCHES("%CHATCLEAN%","^\[Server\] (.+) Limpando (.+) do chao em 5s$");
    SET(@sleep5s);
    SET(@break);
    WAIT(110T);
    UNSET(@sleep5s);
ENDIF;
IFMATCHES("%CHATCLEAN%","(Seu invent.rio foi limpo|Your inventory has been cleared)");
    SET(@sleepclear);
ENDIF;
IFMATCHES("%CHATCLEAN%","(Você j. tem|J. existe) uma home com esse nome, caso queira substitu.-la(,|) digite o comando novamente\!");
    SET(@sleepsubshome);
ENDIF;
IFMATCHES("%CHATCLEAN%","^(A home \"[\d\w_-]+\" foi definida com sucesso\!|Voc. criou a home \"[\d\w_-]+\" neste local.)$");
    SET(@sleepsethome);
ENDIF;
IFMATCHES("%CHATCLEAN%","^Voc. precisa de mais (.*)$");
    SET(@sleeplack);
ENDIF;
IFMATCHES("%CHATCLEAN%","^\*\*Rolou perfeitamente\*\*$");
    SET(@sleeprolled);
ENDIF;
IFMATCHES("%CHATCLEAN%","^Voc. conseguiu manter o encantamento nesse item.$");
    SET(@sleeprepair);
ENDIF;
IFMATCHES("%CHATCLEAN%","^\[Tesouro\] %PLAYER% encontrou um livro: Tesouro N.vel ([\d,.]+)$",@#nivel,1,NONE);
    SET(@sleepbook);
    SET(@break);
ENDIF;
IFMATCHES("%CHATCLEAN%","^(Voc. foi enviado\(a\) para a home \"[\w\d_]+\"\!|Teleportado\(a\)\!)$");
    SET(@sleepteleport);
ENDIF;
IFMATCHES("%CHATCLEAN%","^(Teleportado|Teleported).$");
    SET(@sleepteleport);
ENDIF;
IFMATCHES("%CHATCLEAN%","^(Voce|Voc.) recebeu 1 coin\(s\) por ter ajudado a acabar com o mal\!$");
    SET(@sleepmobkill);
    INC(#mobs_killed,1);
        IFMATCHES("%#mobs_killed%","^[\d]+(.*)(0)$");
            ACHIEVEMENTGET("%#mobs_killed% mobs mortos","397");
                IFMATCHES("%#mobs_killed%","^[\d]+(.*)(00)$");
                    WAIT(65T);
                    ACHIEVEMENTGET("#%&dmc%","397");
                ENDIF;
        ENDIF;
ENDIF;
IFMATCHES("%CHATCLEAN%","^\[CraftLandia\] Voc. matou: (.*)$");
    INC(#onchat_kills,1);
        IF(%#onchat_kills%=1);
            ACHIEVEMENTGET("%#onchat_kills% kill","397:3");
        ELSE;
            ACHIEVEMENTGET("%#onchat_kills% kills","397:3");
        ENDIF;
    WAIT(65T);
    ACHIEVEMENTGET("#%&dmc%","397:3");
ENDIF;
IFMATCHES("%CHATCLEAN%","^(.*) deseja ir at. voc.\! (Use|Digite) \/bring (.+) para aceitar.$",&nick_bring,1,NONE);
    SET(sistema_call);
ENDIF;
IFMATCHES("%CHATCLEAN%","^» (.*) quer ir até voc.\!$",&nick_bring,1,NONE);
    SET(sistema_call);
ENDIF;
IF(sistema_call);
    UNSET(sistema_call);
        IF(!call);
            SET(call);
            SET(&nick_aceitar,"%&nick_bring%");
            ACHIEVEMENTGET("Press. DEL para aceitar","368");
                DO(600);
                    IF((KEY_DELETE)||(KEY_DECIMAL));
                        ECHO("/bring @%&nick_aceitar%");
                        ACHIEVEMENTGET("Aceito :) #%&dmc%","368");
                        BREAK;
                    ENDIF;
                LOOP;
            UNSET(call);
        ENDIF;
ENDIF;

}$$
